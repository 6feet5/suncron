#!/usr/bin/perl -w

# This file is part of SunCron.
#
# SunCron is free software; you can redistribute it and/or modify it 
# under the terms of the GNU Gerenral Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# SunCron is distributed in the hope that it will be usefull,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with SunCron; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#
# Copyright: (C) 2009-2010 Johan Stenarson <johan.stenarson@gmail.com>

#
# Are we installed?
#
use lib (-e '/usr/share/suncron/' ? '/usr/share/suncron/lib' : $ENV{PWD}.'/lib');

use strict;
use warnings;

use Data::Dumper;
use Getopt::Long;
use Pod::Usage;

# Internal modules
use ConfigFile;
use CalcTime;

my $sun_never;
local $SIG{__WARN__} = sub {
    # Catch warning from Astro if sun never sets or rises
    if($_[0] =~ /Sun never (\w*)!*/) {
        $sun_never = $1;
    } else {
        warn $_[0];
    }
};

my $NAME    = 'SunCron';
my $VERSION = '0.1';

my $config_file = '/etc/default/suncron';
my $cron_file = '/etc/cron.d/suncron';

# Parse command line arguments
my ($version, $help, $man, $init, $verbose);
my $result = GetOptions(
        'conf=s'    => \$config_file,
        'cron=s'    => \$cron_file,
        'help'      => \$help,
        'man'       => \$man,
        'verbose'   => \$verbose,
        'version'   => \$version,
        );

pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;
if($version || $verbose) {
    print <<EOF;
$NAME v$VERSION
Copyright (C) 2009-2010 by Johan Stenarson
This is free software; you are free to change it and distribute it.
There is no WARRANTY
EOF
    exit if $version;
}

print "Loading configuration...\n" if $verbose;
my $config = ConfigFile->new($config_file);

print "Creating time expression parser...\n" if $verbose;
my $parser = CalcTime->new( longitude => $config->longitude, latitude => $config->latitude, timezone => $config->timezone );

open(my $cron, ">", $cron_file);

print $cron "#\n# WARNING! This file is automatically generated, changes will be lost\n#\n";

my @rules = @{$config->get_rules()} or die("No rules defined in configuration\n");
for my $rule (@rules) {
    print "Parsing rule: $rule\n" if $verbose;

    my @parts = split(/;/, $rule);
    my $time;

    # Parse condition
    my $result = $parser->evaluate($parts[0]);

    last if defined $sun_never;

    if($result) {
        $time = $parser->evaluate($parts[1]);
    } else {
        $time = $parser->evaluate($parts[2]);
    }
    if(defined $time) {
        print $cron $time->minute()." ".$time->hour()." ".$parts[3]."\n";
    }
}

print $cron "# File is empty because sun never $sun_never\n" if defined $sun_never;

close $cron;

print "Done!\n" if $verbose;

exit;

=head1 NAME

SunCron - Tell cron to run commands at times based on sunset and sunrise

=head1 SYNOPSIS

suncron [options...]

 Options:
    --conf=FILE         filename of configuration file
    --cron=FILE         filename of cron file
    --help              show brief help text then exit
    --man               show full documentation then exit
    --verbose           show verbose information
    --version           output version information and exit

=head1 OPTIONS

=over 

=item B<--conf>

Full path to configuration file. Default is F</etc/default/suncron>.

=item B<--cron>

Where to write cron file. Default is F</etc/cron.d/suncron>.

=item B<--help>

Show a brief help text and exit.

=item B<--man>

Show documentation and exit.

=item B<--verbose>

Show information about what program is doing.

=item B<--version>

Show version number and exit.

=back

=head1 DESCRIPTION

This program is expected to be run daily at midnight and will then calculate
the sunrise and sunset. This information is used when evaluating rules in the
configuration file and the result will be used to generate cron entries in a
cron file. In other words, with this program you'll be able to tell cron to
run commands at sunrise or sunset.

The configuration file is found in '/etc/default/suncron.conf'.
It has a B<Location> section and a B<Rules> section. Each rule consists of four 
parts:

=over

=item B<a condition>

The condition is a clear text formula describing the condition, eg. 'sunset < 17:00' to test if sun sets before 17:00.

=item B<a true statement> and B<a false statement>

These statements is the time to use if condition is true/false, eg. 'sunrise + 03:30' for three and a half hour after sunrise.

=item B<a cron part>

The cron part is a cron line without the minute and hour field, eg. '* * [1-4] root /path/to/command arg'. The resulting cron data is written to /etc/cron.d/suncron (unless you redirect it with the --cron switch).

=head1 REQUIREMENTS

This project depend on the following software:

=over

=item B<Astro::Sunrise>

Perl extension for computing the sunrise/sunset on a given day

=item B<DateTime>

A date and time object

=back

=head1 AUTHOR

Johan Stenarson E<lt>johan.stenarson@gmail.comE<gt>

=head1 BUGS

Probably, but none that I know of.

=head1 COPYRIGHT

Copyright (C) 2009-2010 Johan Stenarson. All rights reserved.
This program is free software; you can redistribute it and/or modify it.

The full text of the license can be found in the copyright file included with this package.

=cut

